#Mantainer: Giovanni Ricciardi <kar98k.sniper@gmail.com>

pkgname=mate-settings-daemon
pkgver=1.5.5
pkgrel=1
pkgdesc="The MATE Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libmatekbd' 'mate-desktop' 'libmatenotify' 'gstreamer0.10-base' 'hicolor-icon-theme')
makedepends=('intltool' 'pkgconfig' 'mate-desktop')
options=('!libtool')
url="http://mate-desktop.org"
install=mate-settings-daemon.install
groups=('mate')
source=(http://pub.mate-desktop.org/releases/1.5/${pkgname}-${pkgver}.tar.xz)
sha256sums=('f9e6493e0bbc7ad5f9da0cd8cbcc7c3ec89fc300ecca22c3b7c9e325f6888df7')


build() {
    cd "$srcdir/$pkgname-$pkgver"
    # Remove --disable-pulse if you want pulseaudio support
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/mate-settings-daemon \
        --disable-static \
        --disable-pulse \
        --disable-schemas-install \
        --enable-polkit || return 1
    make || return 1
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"

    make DESTDIR="${pkgdir}" MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install || return 1

    install -d -m755 "${pkgdir}/usr/share/mateconf/schemas"
    mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
    rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
