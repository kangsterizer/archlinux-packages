#Mantainer: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>

pkgname=mate-control-center
pkgver=1.5.3
pkgrel=1
pkgdesc="The Control Center for MATE"
arch=('i686' 'x86_64')
license=('GPL')
depends=('mate-window-manager' 'libxss' 'desktop-file-utils'
         'mate-settings-daemon' 'libmatekbd' 'libcanberra'
         'sound-theme-freedesktop' 'libunique' 'mate-menus'
         'librsvg') #  'evolution-data-server>=2.29.92'
makedepends=('mate-doc-utils' 'pkgconfig' 'intltool')
options=('!emptydirs' '!libtool')
url="http://mate-desktop.org"
groups=('mate')
install=mate-control-center.install
source=(http://pub.mate-desktop.org/releases/1.5/${pkgname}-${pkgver}.tar.xz)
sha256sums=('ac47d3a003b38baff40327ec36272cd368f19b09eab762af822c2c4344ee99d6')

build() {
    cd "${srcdir}/$pkgname-$pkgver"
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --disable-scrollkeeper \
        --disable-update-mimedb || return 1
    #  --enable-aboutme <- this requires libebook (from evolution)
    make || return 1
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"

    make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

    install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
    mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
    rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas

    # --disable-update-mimedb?
    rm -f "${pkgdir}/usr/share/applications/mimeinfo.cache"
}
