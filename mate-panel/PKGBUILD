#Mantainer: Giovanni Ricciardi <kar98k.sniper@gmail.com>

pkgname=mate-panel
pkgver=1.5.5
pkgrel=1
pkgdesc="The MATE Panel"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libmatewnck' 'mate-menus' 'mate-desktop' 'librsvg' 'hicolor-icon-theme' 'libcanberra' 'libmateweather') # 'evolution-data-server'
makedepends=('mate-doc-utils' 'pkgconfig' 'intltool' 'networkmanager')
options=('!emptydirs' '!libtool')
install=mate-panel.install
source=(http://pub.mate-desktop.org/releases/1.5/${pkgname}-${pkgver}.tar.xz)
sha256sums=('afbda559d464adc29ab570044bddc0cfb383505269e17eb669392ab957740fcd')

url="http://mate-desktop.org"
groups=('mate')

build() {
    cd "${srcdir}/$pkgname-$pkgver"

    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --libexecdir=/usr/lib/mate-panel \
        --disable-scrollkeeper \
        --enable-introspection \
        --enable-matecomponent || return 1
    make || return 1
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"

    make DESTDIR="${pkgdir}" install || return 1
}
